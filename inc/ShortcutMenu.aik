/**
 *@file    ShortcutMenu.aik
 *@author  teshorse
 *@date    2011.09.15
 *@brief   生成快捷菜单
 *
 * 这里定义的Lables也可能被其他文件引用和调用
 */

; #Include ../../
#include .\inc\Window.aik

CreateMenuByIni( menu_, param_="A", inimem_="", bShow=true )
{
	Local inifile, oldclip, NeedClipMenuList, menuArray, active_id 
	Local bNeedClip=false               ;; 在弹出菜单前，是否需要先执行ctrl+c复制
	; global g_iniMenumem               ;; ShortcutMenu.ini 菜单内容
	; global g_iniMenumem_alluser		;; ..\ShortcutMenu.ini 菜单内容, 公共的菜单配置文件
	oldclip = %ClipboardAll%
	inifile = ShortcutMenu.ini

	ifNotExist ShortcutMenu.ini
		InitShortcutMenuIniFile()

	if menu_ =
		return false

	if param_ =
	{
		WinGet, active_id , id, A
		g_ATitle = ahk_id %active_id%
	}
	else 
		g_ATitle := param_

	if inimem_ =
		g_iniMenumem := inifileread( inifile )
	else
		g_iniMenumem := inimem_


	if g_iniMenumem_alluser =
		g_iniMenumem_alluser := inifileread( "..\ShortcutMenu.ini" )


	if ( g_iniMenumem == "" && g_iniMenumem_alluser == "" )
	{
		return false
	}

	;; 如果是需要复制选中文本的菜单，则先复制
	g_clip =                                ;; 复制的内容
	NeedClipMenuList = 编辑选中文本菜单
	Clipboard =

	if ( InStrList( NeedClipMenuList, menu_, "|" ) > 0 )
	{
		send ^c
		bNeedClip := true
	}
	else ifinstring menu_, ^c           ;; 如果菜单名中包含^c字样，则发送CTRL+C复制
	{
		send ^c
		bNeedClip := true
	}    


	;; 创建菜单
    g_iMenu  := 0                       ;; 菜单临时编号
    g_iMenuList =                       ;; 临时存储已经创建好的菜单名列表，用于判断某个菜单是否已经创建过
	bRet := CreateMenu( menu_, g_iniMenumem )


	;; 显示菜单
	if ( bRet && bShow )
		Menu, %menu_%, show

	;; 清空菜单缓存内容
	if g_iMenu > 0
	{
		loop %g_iMenu%
		{
			menuArray = g_menu%a_index%Array
			ClearArray( menuArray )
		}
	}
	g_iMenu  := 0
	

	;; 如果使用的剪贴板，则还原剪贴板的旧内容
	if not bNeedClip
	{
		Clipboard := oldclip
	}
	return bRet
}


;;---------------------------------------------------------------------------
;; 菜单 执行
CreateMenu( menu_, inimem_ )
{
	Local menuArray
	Local n, line, keyName, keyValue, opt, cmd, isDefine
	;; global g_iniMenumem_alluser // ..\ShortcutMenu.ini 菜单内容, 公共的菜单配置文件

	;; g_menu%g_iMenu%Array 临时存储从ShortcutMenu.ini文件中menu_分节中的所有菜单项
    g_iMenu++                           ;; 菜单临时编号
	menuArray  = g_menu%g_iMenu%Array	;; 临时存储从配置文件中读取的该菜项

	;; 如果该菜单已经创建过了（g_iMenuList已经记录了该菜单名），则不用再创建直接返回
	if InStrList( g_iMenuList, menu_, "|" ) > 0
		Return
	Else
		StrListAdd( g_iMenuList, menu_, "|" )

	isDefine := false ;; 默认假设配置文件中没有定义指定菜单

	;; 先检查inimem_是否有定义菜单menu_，如果没有，则尝试从公共的菜单配置文件中读取
	if  SecArrayFromIniMem( inimem_, menu_, menuArray ) 
	{
		isDefine := true
	}
	else ifexist ..\ShortcutMenu.ini
	{
		if SecArrayFromIniMem( g_iniMenumem_alluser, menu_, menuArray ) > 0
			isDefine := true
	}


	;; 如果在配置文件中定义了指定菜单，则创建之
	if isDefine
	{
		n := %menuArray%0
	
		loop %n%
		{
			line := %menuArray%%a_index%
			IfNotInString line, =
				Continue

			StrSplit2Sub( line, "=", keyName, keyValue )
	;		tooltip 	menuArray(%menuArray%) StrSplit2Sub( line`, "="`, %keyName%`, %keyValue% )`n%line%

			if keyName =
				Continue
			if keyValue =
				Continue

			;; 分解选项与菜单项
			cmd := cmdStringSplit( keyValue, opt )

			;; 分析菜单项选项
			If InStrList( opt, "0", "|" ) > 0
			{
				Continue 		;; 设置无效，则不显示该菜单项
			}
			Else If InStrList( opt, "submenu", "|" ) > 0
			{   ;; 如果是子菜单， 则递归创建之，子菜单名为cmd
				CreateMenu( cmd, inimem_ )
				Menu,%menu_%, add, %keyName%, :%cmd%
				ifinstring cmd, ^c           ;; 如果菜单名中包含^c字样，则发送CTRL+C复制
				{
					if clipboard =
						send ^c	
				}
			}
			Else
			{
		;		msgbox Menu`,%menu_%`, add`, %item%
				if keyName = ------
					Menu,%menu_%, add
				else if IsValidCmd( keyValue )
				{
					Menu,%menu_%, add, %keyName%, 【通用菜单通用处理模块】
				}
			}
		}
		return true
	}
	return false
}

【通用菜单通用处理模块】:
	if g_iniMenumem =
		return
	g_cmdString := FindFromIniMem( g_iniMenumem, A_ThisMenu, A_ThisMenuItem, "" )
	;; 如果没有命令，则尝试从公共配置文件中获取命令
	if g_cmdString =
	{
		g_cmdString := FindFromIniMem( g_iniMenumem_alluser, A_ThisMenu, A_ThisMenuItem, "" )
	}
	if g_cmdString =
	{
		msgbox 菜单[%A_ThisMenu%]下的<%A_ThisMenuItem%> 没有可执行命令！
	}
	else
	{
		g_thisMenu := A_ThisMenu
		g_ThisMenuItem := A_ThisMenuItem
		sleep 100
		run_cmd( g_cmdString ) 
	}
	return


【菜单调整当前窗口大小】:
	if g_is_调整窗口大小
	{
		Menu, 调整窗口大小, DeleteAll
	}

	if CreateMenuByIni( "调整窗口大小", "A", "", false )
	{
		g_is_调整窗口大小 := true

		var_list := ReadTempIni( "菜单调整当前窗口大小", "recent" )
		if var_list <>
		{
			StringSplit, OutputArray, var_list , |
			loop %OutputArray0%
			{
				var_item := OutputArray%a_index%
				if var_item =
					continue
				Menu, 调整窗口大小, add, &%a_index% 调整到 ( %var_item% ) , 【调整窗口大小_最近大小】
			}
		}
		Menu, 调整窗口大小, show
	}
	return

【调整窗口大小_最近大小】:
	; msgbox  % A_ThisMenuItem .
	Sleep 100
	var_temp := StrMid2Sub( A_ThisMenuItem, "(", ")" )
	if var_temp <>
	{
		StrSplit2Sub( var_temp, "*", W, H )
		∑调整当前窗口为预设大小( W, H )	
	}
	return


【添加当前窗口大小到最近列表】:
	WinGetPos , , , W, H, A
	msgbox 4, 确认添加当前窗口大小到最近列表, 当前窗口大小（%W% * %H% ）`, 确定添加？
	ifMsgbox Yes
	{
		∑保存窗口大小到最近列表( W, H )
	}
	return


【管理窗口大小最近列表】:
	var_root := ∑获取根目录()
	var_file = %var_root%\bin\dict\dict.ahk
	var_param = 
		(Ltrim
		file:temp.ini
		seclist:菜单调整当前窗口大小
		title:菜单调整当前窗口大小
		cursec:菜单调整当前窗口大小
		curkey:recent
		)
	run_ahk( var_file, var_param, a_workingdir )		
	return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Lables

【右键菜单_整齐地在行尾添加字符】:
		var_root := ∑获取根目录()
		var_file = %var_root%\bin\Doxgen\复制并在每行末尾整齐地添加行尾注释号.ahk
		var_param = |%Clipboard% 		;; 参数=注释符号|选中的文本内容，这里注释符号为空
		run_ahk( var_file, var_param )
	Return

【右键菜单_生成doxyGen风格的函数注释】:
		var_root := ∑获取根目录()
		var_file = %var_root%\bin\Doxgen\复制函数声明得到函数注释.ahk
		run_ahk( var_file, Clipboard )
	Return

【右键菜单_注释选中的内容】:
	block_selected_text("/*", "*/")
	Return

【右键菜单_在行首添加字符】:
	∑在选中的行行首添加字符()
	Return


【测试1】:
	MsgBox 11111111111
	Return

【测试2】:
	MsgBox 22222222
	Return


【测试3】:
	MsgBox 33333333
	Return


【测试4】:
	MsgBox 4444444444444
	Return



【最大化当前窗口】:
	Sleep 100
    ∑最大化窗口( "A" )                ;; 标题名称不能用g_ATitle, 因为该功能模块不仅仅被快捷菜单调用
	return

【最小化当前窗口】:
	Sleep 100
	∑最小化窗口( "A" )
	return

【还原当前窗口】:
	Sleep 100
	∑还原窗口( "A" )
	return
	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 相关函数
∑在选中的行行首添加字符()
{
	clipboard =
	sleep 100
	send ^c
	sleep 200
	if clipboard =
	 	return
	StringReplaceAll(clipboard, "`r`n", "`n")
	StringReplaceAll(clipboard, "`r", "`n")

	var_input := MyInput( "行首增加字符", "请输入要添加到行首的字符（串）" )
	if var_input =
		return
	StringReplaceAll(var_input, "``t", "`t")
	StringReplaceAll(var_input, "\t", "`t")
	StringReplaceAll(var_input, "tab", "`t")
	StringReplaceAll(var_input, "``r``n", "`r`n")
	StringReplaceAll(var_input, "``n", "`r`n")
	StringReplaceAll(var_input, "``r", "`r`n")
	StringReplaceAll(var_input, "enter", "`r`n")

	var_clip =
	AutoTrim, Off
	loop parse, clipboard, `n
	{
		if a_loopfield =
		{
			var_clip = %var_clip%`n
			continue
		}


		if var_clip =
			var_clip = %var_input%%a_loopfield%
		else
			var_clip = %var_clip%`n%var_input%%a_loopfield%
	}
	clipboard := var_clip
	AutoTrim, On
	sleep 200
	send ^v
}


【调整窗口大小_自定义大小】:
	Sleep 100

	str_input := myinput( "设置窗口宽度和高度", "请指定宽度和高度: Width * Height", "800 * 600" )
	if str_input =
		return
	ifNotInstring str_input, *
		return

	stringsplit, OutputArray, str_input, *
	W := OutputArray1
	H := OutputArray2

	∑调整当前窗口为预设大小( W, H )	
    Return


∑调整当前窗口为预设大小( W, H )
{
	WinGetPos , , Y, , taskH, ahk_class Shell_TrayWnd		;; 获得任务栏的高
	deskH := A_ScreenHeight - taskH     ;; 除去任务栏后的桌面高度
	if ( H > deskH ) 
			H := deskH

	sleep 100
	∑调整窗口大小( W, H, "A" )

	∑保存窗口大小到最近列表( W, H )
}

∑保存窗口大小到最近列表( W, H )
{
	;; 保存最近调整的窗口大小，以便添加到快捷菜单
	StrTrim( W )
	StrTrim( H )
	var_item = %w% * %h%
	var_list := ReadTempIni( "菜单调整当前窗口大小", "recent" )
	∑Add字符串队列( var_list, var_item, "|", true, 5 )
	WriteTempIni( "菜单调整当前窗口大小", "recent", var_list )
}

【移动当前窗口到屏幕左上角】:
	Sleep 100
	WinMove, A,, 0, 0
	return

【移动当前窗口到屏幕中央】:            
	Sleep 100
	CenterWindow( "A" )
	return

【移动当前窗口到屏幕右下角】:
	Sleep 100
	WinGetPos , , , W, H, A
	X := a_ScreenWidth - W - 5
	Y := GetDeskHeight() - H - 5
	WinMove, A,, %X%, %Y%
 	return

【移动当前窗口到屏幕右上角】:
	Sleep 100
	WinGetPos , , , W, H, A
	X := a_ScreenWidth - W - 5
	WinMove, A,, %X%, 0
 	return

【移动当前窗口到屏幕左下角】:
	Sleep 100
	WinGetPos , , , W, H, A
	Y := GetDeskHeight() - H - 5
	WinMove, A,, 0, %Y%
 	return	

【置顶当前窗口】:
	Sleep 100
	WinSet, AlwaysOnTop, Toggle, A
	if IsWinTopMost( "A" )
		tipx( 20, "窗口最前", 1000 )
	else 
		tipx( 20, "取消置顶", 1000 )
	return


【创建菜单外部工具编辑选中文本】:
	g_EditPath =
	if not CreateMenuByIni( "菜单外部工具编辑选中文本", "A", "", false )
	{
		tooltip7("菜单未定义[菜单外部工具编辑选中文本]")
		return
	}

	;; 使用VIM编辑
	regread var_vimpath, HKEY_LOCAL_MACHINE, SOFTWARE\Vim\GVim, path
	if var_vimpath <>
	{
		Menu, 菜单外部工具编辑选中文本, add, &VIM, 【使用VIM编辑选中的文本】
	}
	;; 使用EmEditor编辑
    var_read := read_ini( "launchy.ini", "file", "emedit", "" )
    if var_read <>
    {
		var_pos := instr( var_read, "|" )
		if var_pos > 0
    	 	StringLeft var_empath, var_read, var_pos-1
		else
			var_empath := var_read

		ifExist %var_empath%
		{
			g_EditPath := var_empath
			Menu, 菜单外部工具编辑选中文本, add, &EmEditor, 【使用指定文本编辑器打开选中文本】
		}
    }


	;; 使用UE编辑
    var_read := read_ini( "launchy.ini", "file", "UltraEdit", "" )
    if var_read <>
    {
		var_pos := instr( var_read, "|" )
		if var_pos > 0
    	 	StringLeft var_uepath, var_read, var_pos-1
		else
			var_uepath := var_read

		ifExist %var_uepath%
		{
			g_EditPath := var_uepath
			Menu, 菜单外部工具编辑选中文本, add, &UtralEidt, 【使用指定文本编辑器打开选中文本】
		}
    }
	;; 使用记事本编辑
	Menu,  菜单外部工具编辑选中文本, add, 记事本(&N), 【使用NotePad编辑选中的文本】
	return

【使用指定文本编辑器打开选中文本】:
	if g_EditPath <>
	{
		var_root := ∑获取根目录()
		var_file = %var_root%\bin\文本文件处理\用指定工具编辑选中文本.ahk
		var_param = `"%g_EditPath%`"
		run_ahk( var_file, var_param )
	}
	return


【菜单外部编辑选中文本】:
	gosub 【创建菜单外部工具编辑选中文本】
	Menu , 菜单外部工具编辑选中文本, show
	return
	

【菜单编辑选中文本】:
	send ^c
	if not CreateMenuByIni( "菜单编辑选中文本", "A", "", false )
	{
		tooltip7("菜单未定义[菜单外部工具编辑选中文本]")
		return
	}

	if CreateMenuByIni( "编辑选中代码菜单", "A", "", false )
		menu 菜单编辑选中文本, add, 代码 &d, :编辑选中代码菜单 

	gosub 【创建菜单外部工具编辑选中文本】
	menu 菜单编辑选中文本, add, 外编 &e, :菜单外部工具编辑选中文本 
	menu 菜单编辑选中文本, show
	return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 增强右捷菜单
【增强右捷菜单】:
	if ( IsExploreWindow() )
	{
		var_file := ∑获取根目录()
		var_file = %var_file%\bin\explore\ExploreMenu.ahk
		run_ahk( var_file )
	}
	else
	{
		CmdStringTip("")
	}
	return


InitShortcutMenuIniFile()
{
	var_temp2 =
(
[编辑选中代码菜单]
整齐地在行尾添加字符(&E)=1|gosub)【右键菜单_整齐地在行尾添加字符】
生成doxyGen风格的函数注释(&F)=1|gosub)【右键菜单_生成doxyGen风格的函数注释】
/* 选中的内容 */ (&/)=1|gosub)【右键菜单_注释选中的内容】
在行首添加字符(&A)=1|gosub)【右键菜单_在行首添加字符】
整理代码空格 (&Z)=1|gosub)【复制并调整选中代码的空格】

[调整窗口到预设大小]
&1 ( 350 * 250 )  =1|func)∑调整当前窗口为预设大小(350,250)
&2 ( 640 * 480 )  =1|func)∑调整当前窗口为预设大小(640,480)
&3 ( 800 * 600 )  =1|func)∑调整当前窗口为预设大小(800,600)
&4 ( 1204 * 768 ) =1|func)∑调整当前窗口为预设大小(1204,768)
&5 ( 1152 * 864 ) =1|func)∑调整当前窗口为预设大小(1152,864)
&6 ( 1280 * 768 ) =1|func)∑调整当前窗口为预设大小(1280,768)
&7 ( 1280 * 1024 )=1|func)∑调整当前窗口为预设大小(1280,1024)
&8 ( 1600 * 1200 )=1|func)∑调整当前窗口为预设大小(1600,1200)
&9 ( 1680 * 1050 )=1|func)∑调整当前窗口为预设大小(1680,1050)


[调整窗口大小]
最大化当前窗口 &X=1|gosub)【最大化当前窗口】
最小化当前窗口 &N=1|gosub)【最小化当前窗口】
还原当前窗口 &R=1|gosub)【还原当前窗口】
------=1)
自定义窗口大小 &C=1|gosub)【调整窗口大小_自定义大小】
添加当前窗口大小 &S=1|gosub)【添加当前窗口大小到最近列表】
管理最近窗口大小 &M=1|gosub)【管理窗口大小最近列表】
------=1)
&0 预设大小=1|submenu)调整窗口到预设大小

[窗口常用菜单]
窗口最前面 &T=1|gosub)【置顶当前窗口】
窗口透明度 &A=1|gosub)【调节窗口透明度】
◎屏幕中央 &5=1|gosub)【移动当前窗口到屏幕中央】
移动至 &M=1|submenu)窗口移动至屏幕角落

[窗口移动至屏幕角落]
I屏幕左上角 &7=1|gosub)【移动当前窗口到屏幕左上角】
J屏幕右上角 &9=1|gosub)【移动当前窗口到屏幕右上角】
K屏幕右下角 &3=1|gosub)【移动当前窗口到屏幕右下角】
L屏幕左下角 &1=1|gosub)【移动当前窗口到屏幕左下角】


[^c搜索选中文本]
&Baidu=1|run)http://www.baidu.com/s?wd=$clip$
&Google=1|run)http://www.google.com/search?q=$clip$&hl=en&sourceid=gd&rlz=1Q1GGLD_zh-CNCN421&aq=t
&Enkoo=1|run)http://www.engkoo.com/#$clip$
&i词霸=1|run)http://www.iciba.com/$clip$


[菜单编辑选中文本]
剪切 &x=1|send)^x
复制 &c=1|send)^c
搜索 &s=1|submenu)^c搜索选中文本



[菜单外部工具编辑选中文本]
&UltraEdit=1|gosub|launchy:UltraEdit)【使用外部编辑器编辑选中的文本】
&EmEdit=1|gosub|launchy:em)【使用外部编辑器编辑选中的文本】


[^c菜单复制文本作为配置]
飞扬命令窗口 &R=1|gosub)【添加飞扬命令窗口】
飞扬命令串   &C=1|gosub)【复制为飞扬命令串】
飞扬个性短语 &I=1|gosub)【添加飞扬个性短语】

)

	var_root := ∑获取根目录()
	var_root = %var_root%/Users/ShortcutMenu.ini
	var_temp := IniFileRead( var_root )
	FileAppend %var_temp%, ShortcutMenu.ini
}


#include ./inc/common.aik
#include ./inc/inifile.aik
#Include ./inc/cmdstring.aik
#include ./inc/path.aik
